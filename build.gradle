plugins {
    id "java-library"
    id "maven-publish"
    id "eclipse"
}

group = "io.liftbridge"
version = "0.1.0-alpha0"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir "src/gen/java"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                scm {
                    connection = "scm:git:https://github.com/caioaao/java-liftbridge.git"
                    developerConnection = "scm:git:git@github.com:caioaao/java-liftbridge.git"
                    url = "https://github.com/caioaao/java-liftbridge"
                }

                licenses {
                    license {
                        name = "Apache 2.0"
                        url = "https://opensource.org/licenses/Apache-2.0"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "myRepo"
            url = "file://${buildDir}/repo"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api "io.grpc:grpc-api:1.26.0"

    implementation "io.grpc:grpc-stub:1.26.0"
    implementation "io.grpc:grpc-protobuf:1.26.0"
    implementation "com.google.protobuf:protobuf-java:3.11.0"

    testImplementation "junit:junit:4.12"
    testImplementation "io.grpc:grpc-netty:1.26.0"
    testImplementation "org.apache.commons:commons-lang3:3.9"
    testImplementation 'org.awaitility:awaitility:4.0.1'
}

jar {
    manifest {
        attributes("Implementation-Title": project.name,
                   "Implementation-Version": project.version)
    }
}

test {
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"

    }
}